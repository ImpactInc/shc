configurations {
    [compileAndJar, compile, lib, testCompile].each {
        it.exclude module: "servlet-api"
        it.exclude module: "servlet-api-2.5"
        it.exclude module: "servlet-api-2.5-20081211"
        it.exclude group: "xml-apis"
        it.exclude module: "jcl-over-slf4j"
        it.exclude module: "jul-over-slf4j"
        it.exclude module: "log4j-over-slf4j"
        it.exclude module: "log4j-api"
        it.exclude module: "log4j-core"
        it.exclude group: "org.eclipse.jetty.orbit"
        it.exclude group: "com.sun.jersey"
        it.exclude module: "guava-jdk5"
        it.exclude module: "guava-jdk5-13.0"
        it.exclude group: "io.netty", module: "netty"
    }
}
configurations.all {
    resolutionStrategy {
        force "javax.servlet:javax.servlet-api:$servletApi"
        force "com.fasterxml.jackson.core:jackson-core:$jacksonVer"
        force "com.fasterxml.jackson.core:jackson-databind:$jacksonVer"
        force "joda-time:joda-time:$jodaVer"
        force "org.slf4j:slf4j-api:$slf4jVer"
        force "org.slf4j:slf4j-log4j12:$slf4jVer"
        force "com.google.guava:guava:$guavaVer"
        // TODO: Attribution wanted this older version
        // force "com.google.guava:guava:12.0.1"
        force "org.apache.avro:avro:$avroVer"
        force "xml-apis:xml-apis:1.0.b2"
        force "org.apache.commons:commons-lang3:$commonsLang3Ver"
        force "io.netty:netty-all:$nettyVer"

        eachDependency { DependencyResolveDetails details ->
            // force "javax.servlet:javax.servlet-api:$servletApi" not working for javax.servlet:servlet-api:2.5 since different module name
            if (details.requested.group == "javax.servlet" && details.requested.version == "2.5") {
                details.useTarget group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
            }
            // exclude module: "servlet-api-2.5-20081211" not working for org.mortbay.jetty:servlet-api:2.5-20081211
            if (details.requested.version == "2.5-20081211") {
                details.useTarget group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
            }
        }
    }
}